##############################################################################
#
# The Ossuary, a Tomb en miniature (Zaba)
#
#
# The Ossuary tries to pose an unusual early challenge: many traps and slow
# monsters, making the player choose between pressing on towards the loot or
# leaving the vault.
#
# Flavour: Mini versions of Tomb and otherwise crypt like.
#
##############################################################################

# BCADDO: Depth weights will need changing as shit rearranges/shortens.
{{
function ossuary_setup_features(e)
  e.nsubst("1 = 2:1 / *:0")
  e.nsubst("0 = 4:0 / *:)")
  e.nsubst("5 = 2:5 / *:%")
  e.nsubst("4 = 5:4 / */#")
  if you.in_branch("D") then
    e.kmons("0 = mummy")
    e.kmons("1 = guardian mummy")
    e.kmons("4 = scorpion")
	e.kmons("5 = wraith")
	e.kmons(") = mummy")
	e.kmons("% = phantom")
	e.kmons("# = jackal / scorpion / nothing w:30")
  elseif you.in_branch("Lair") or you.in_branch("Orc") then
    e.kmons("0 = guardian mummy")
    e.kmons("1 = mummy priest / guardian mummy")
    e.kmons("4 = bone scorpion")
	e.kmons("5 = eidolon / freezing wraith")
	e.kmons(") = guardian mummy / mummy")
	e.kmons("% = wraith / phantom")
	e.kmons("# = scorpion / bone scorpion / nothing w:30")
  else
    e.kmons("0 = guardian mummy")
    e.kmons("1 = mummy priest")
	e.kmons("4 = emperor scorpion")
	e.kmons("5 = revenant w:30")
	e.kmons(") = guardian mummy")
	e.kmons("% = eidolon / freezing wraith / wraith")
	e.kmons("# = scorpion / emperor scorpion / nothing w:30")
  end
  if you.in_branch("Shoals") or you.in_branch("Swamp") then
    local flood_x, flood_y, n = -1, -1, 0

    -- Find where to start the flood.
    for x = 0, e.width() - 1 do
      for y = 0, e.height() - 1 do
        if e.mapgrd[x][y] == '.' or e.mapgrd[x][y] == "'" or e.mapgrd[x][y] == '`' then
          n = n + 1
          if crawl.one_chance_in(n) then
            flood_x, flood_y = x, y
          end
        end
      end
    end

    -- Flood it!
   e.flood(flood_x, flood_y, crawl.random_range(e.width()/2, e.width()), function(x, y, distance)
      local c = e.mapgrd[x][y]

   local replacements = {
     ['.'] = { {'r', 1}, {'.', 3}, {'<', 8} },
     ["'"] = { {'r', 1}, {"'", 3}, {'W', 4} },
     ["~"] = { {'r', 1}, {'~', 3}, {'W', 4} },
	 ['^'] = { {'r', 1}, {'^', 3}, {'W', 4} },
	 ['`'] = { {'r', 1}, {'`', 3}, {'W', 4} },
     ['c'] = { {'c', 1}, {':', 4} },
     ['+'] = { {'+', 10}, {'W', 4} },
     ['0'] = { {'!', 1} },
     ['1'] = { {'?', 2}, {'!', 1}, {'1', 4} },
     ['2'] = { {'&', 1}, {'2', 2} },
     ['3'] = { {'&', 1}, {'3', 2} },
    }

    if replacements[c] then
        e.mapgrd[x][y] = util.random_choose_weighted(replacements[c])
    end

    if c == '+' or c == 'v' then
        return 0
    end

    return distance - 1
    end)
  end
  
  if you.in_branch("Shoals") then
    e.subst("? = 67")
	e.subst("& = &7")
	e.kmons("& = merfolk / merfolk siren / merfolk impaler w:5 / merfolk javelineer w:5")
	e.subst(": = cx")
	e.subst("< = W")
	e.orient("float")
	e.ftile("r = FLOOR_SAND")
  elseif you.in_branch("Swamp") then
    e.kmons("& = vampire mosquito / deathcap w:2 / tyrant leech w:3")
	e.kmons("? = death drake")
	e.subst("< = Wr")
	e.subst(": = tw")
	e.ftile("r = FLOOR_SWAMP")
	e.subst("X = t")
	e.kfeat("= = floor")
	e.kfeat("+ = floor")
	e.kfeat("t = mangrove")
  end
  if you.in_branch("Spider") then
    e.subst("3 = 2")
	e.nsubst("247 = 1::")
	e.kmons(": = death scarab band")
    e.kmons("1 = mummy priest / entropy weaver")
  end
  e.tags("no_monster_gen water_ok")
  e.ftile(".'abcdefghijklmnopqstuvwxyzABCDEFGHIJKLMNOPQRSTUVWXYZ12345<>67890!@#$%^&*()|~{}+- = floor_sandstone")
  e.ftile("= = floor_sandstone")
  e.tile("c = wall_tomb")
  e.kmons("2 = zombie")
  e.kmons("3 = skeleton")
  e.kmons("6 = spectral thing")
  e.kmons("7 = simulacrum")
  e.kmons("! = bog body")
  e.kfeat("GHI = sarcophagus")
  e.kfeat("J = granite_statue")
  e.tile("G = DNGN_SARCOPHAGUS_SEALED")
  e.tile("H = DNGN_SARCOPHAGUS_PEDESTAL_RIGHT")
  e.tile("I = DNGN_SARCOPHAGUS_PEDESTAL_LEFT")
end

function callback.badplayer_boxes_open_up(data, triggerable, triggerer, marker,
                                          ev)
  if not data.triggered then
    data.triggered = true
    for slave in iter.slave_iterator("disappearing_wall", 1) do
      dgn.terrain_changed(slave.x, slave.y, "floor", false)
    end
    crawl.mpr("There is a click, and some walls slide away!", 'warning')
  end
end
}}

default-depth: D:3-, !D:$, Lair, !Lair:$, Spider, !Spider:$, Swamp, !Swamp:$, Shoals, !Shoals:$

NAME:   enter_ossuary_1
TAGS:   transparent extra uniq_miniossuary
NSUBST: . = 1:1 / 4:2 / *:p
SUBST:  ' = ~..
KFEAT:  ~ = alarm trap / congregation trap / net trap
CLEAR:  p
: ossuary_setup_features(_G)
MAP
.....
.'''.
.'}'.
.'''.
.....
ENDMAP

NAME:    enter_ossuary_2
TAGS:    transparent extra uniq_miniossuary
SUBST:   . = zzz.
SUBST:   r = ...z
NSUBST:  ' = 2:~ / 1:1 / *:p
SUBST:   p = 234..
CLEAR:   z
KFEAT:   ~ = alarm trap / congregation trap
: ossuary_setup_features(_G)
MAP
  rrrrr
 .rJrJr.
 .rrrrr.
..cc+cc.
.cc'''cc.
.cH'}'Hc.
.cc'''cc.
..ccccc..
 .......
ENDMAP

NAME:   enter_ossuary_4
TAGS:   transparent extra uniq_miniossuary
SUBST:  . = .3ppp
SUBST:  ' = .$$
NSUBST: ~ = 1:~ / 1:^
KFEAT:  ~ = alarm trap
KFEAT:  ^ = congregation trap
CLEAR:  p
: ossuary_setup_features(_G)
MAP
 ...........
 ..ccccccc..
...c'G}G'c...
..cc'''''cc..
..c'~c'c~'c..
..cc1c'c1cc..
...ccc'ccc...
 ...J.@.J...
  .........
   .......
    .....
ENDMAP

NAME:   enter_ossuary_5
TAGS:   transparent extra uniq_miniossuary
NSUBST:  ' = 2:1 / 1:~ / |
SUBST: . = 334.pppp
NSUBST: p = 1:0 / *:p
CLEAR: p
KFEAT: ~ = congregation trap
: ossuary_setup_features(_G)
MAP
     .
    ...
   ..c..
  ..ccc..
 ..cc~=c..
..cc1}|cc..
 ..cc1cc..
  ..ccc..
   ..c..
    ...
     .
ENDMAP

NAME:   gup_ossuary_entry_pyramid
TAGS:   transparent extra uniq_miniossuary no_rotate no_vmirror no_hmirror
KITEM: ' = *
NSUBST: ' = 2:1 / 2:0 / 2:4 / *:.
SUBST: . = 233..pppppppp
CLEAR: p
: ossuary_setup_features(_G)
MAP
       .
      ...
     ..c..
    ..ccc..
   ..cc|cc..
  ..cc'''cc..
 ..ccI'''Hcc..
..ccccc=ccccc..
....J.....J....
  .... @ ....
ENDMAP

NAME: nicolae_ossuary_entry_catacombs
TAGS:  extra uniq_miniossuary
SUBST: . = 33..ppppp
NSUBST: A = + / c, B = + / c, C = + / c, s = 1:| / 2:2 / 2:4 / *:4*ddd
NSUBST: ` = 1:1 / 2:2 / 3:34 / *:...
KFEAT: ~ = alarm trap / congregation trap
CLEAR: p
: ossuary_setup_features(_G)
MAP
........
..........
...ccccccccc
..@+'0'csAsc
...ccc+c+cAc
...cs+```+sc
...ccc`~`ccc
 ..cs+```+sc
 ..cCc+c+cBc
   csCscsBsc
   ccccccccc
ENDMAP

NAME: nicolae_ossuary_entry_false_entrances
TAGS: extra uniq_miniossuary
NSUBST: O = 1:} / 1:| / *:d
SHUFFLE: AB/AB/AB/BA
SUBST: . = 3.ppp
SUBST: ' = 234$$$..
SUBST: A = m, B = c
CLEAR: p
: ossuary_setup_features(_G)
MAP
 ...     ...
.@..xxxxx..@.
..+ccccccc+..
 .c'''c'''c.
 xc'O'A'O'cx
 xc''1B1''cx
 xccABBBAccx
 xc''1B1''cx
 xc'O'A'O'cx
 .c'''c'''c.
..+ccccccc+..
.@..xxxxx..@.
 ...     ...
ENDMAP

NAME: nicolae_ossuary_entry_giza
TAGS: transparent extra uniq_miniossuary no_rotate no_vmirror no_hmirror
SUBST: . = 34.pppppp
SUBST: ' = 34....pp
CLEAR: p
KITEM: 1 = *
: ossuary_setup_features(_G)
MAP
          .
         ...
        ..c..
       ..ccc..
      ..cc1cc..
     ..ccc+ccc..
    ..c.'''''.c..
   ..ccc.'''.ccc..
  ..cc1cc.'.cc1cc..
 ..ccc+ccc.ccc+ccc..
.....................
ENDMAP

# Doesn't run setup features because we don't want it having full place weight of normal ossuaries.
NAME: nicolae_ossuary_entry_monument
TAGS: transparent extra decor
NSUBST: ' = 1:4 / *:$
FTILE:  4.GH = FLOOR_SANDSTONE
KFEAT: H = sarcophagus
: if you.in_branch("D")  then
  KMONS: 4 = scorpion
: else
  KMONS: 4 = emperor scorpion
: end
MAP
  G
  '
G'H'G
  '
  G
ENDMAP

NAME: nicolae_ossuary_entry_plus
TAGS: transparent extra uniq_miniossuary
: if crawl.coinflip() then
SUBST: Dd = =, o = |, p = '
: else
SUBST: d = =, D = c, o = '
NSUBST: p = 1:| / *:'
: end
NSUBST: ' = 1:1 / 2:0 / *:4..
SUBST: . = ppppp...3
CLEAR: p
: ossuary_setup_features(_G)
MAP
    .....
    .J.J.
    .....
   ..cDc..
....3cpc3....
.J.ccc'ccc.J.
...Dp'o''d...
.J.ccc'ccc.J.
....3cpc3....
   ..cDc..
    .....
    .J.J.
    .....
ENDMAP

NAME: lightli_ossuary_entry_decrepit_ruins
TAGS: transparent extra uniq_miniossuary
SUBST: . : .p
SUBST: ' = 4~..
: if crawl.coinflip() then
TAGS: ruin
SUBST: . = .ppp
: end
NSUBST: 4 = 1:1 / 2:0 / *:34
KFEAT: ~ = alarm trap
NSUBST: a = 1:' / *:c
CLEAR: p
: ossuary_setup_features(_G)
MAP
 ..   ..
.JcccccJ.
.cc'*'cc.
 caaaaac
 c'''''c
 c'''''c
.cc'''cc.
.Jcc'ccJ.
 ...@...
ENDMAP

NAME: chequers_ossuary_entry_cave
TAGS: transparent extra uniq_miniossuary no_rotate no_hmirror
ORIENT: north
NSUBST: ' = 2:~ / 3:4 / *:$
NSUBST: ~ = 1:^ / 1:~
KFEAT: ~ = alarm trap / floor
KFEAT: ^ = congregation trap / floor
: ossuary_setup_features(_G)
MAP
 ccccccc
ccI'''Hcc
cG1'|'1Gc
ccI'.'Hcc
 ccc.ccc
   c.c
   c.c
   J@J
ENDMAP

#### The portal vaults ####################################
# Sort destination maps by WEIGHT.
###########################################################
# Common: WEIGHT 40
###########################################################

NAME:    ossuary_tomb_1
TAGS:    uniq_ossuary
KMASK:   ' = no_item_gen
SHUFFLE: dwyz
SHUFFLE: WYZ
SUBST:   w=2, y=2
NSUBST:  z= 1:1 / *:3
SUBST:   . = '''.
CLEAR:   .
KFEAT:   T = alarm trap / floor
# loot: 12.5 items
ITEM:    any scroll w:2 / any scroll q:2 w:3 / \
         any potion w:4 / any potion q:2 w:6
: ossuary_setup_features(_G)
MAP
  cccccccccccccc
  c.22*c'''cd**c
  c1cccc'''cdddc
  c''''''''+cccc
  cccccc'''''''c
  c'zzzc'''2'''c
  c'cz}c'3'''3'c
  c'cccc'''2'''c
 .c''''''''''''c.
..c'ccccTTcccc'c..
..c'cy}cTTc}wc'c..
..c'yyycTTcwww'c..
..ccccccTTcccccc..
......''''''......
    ..'G''G'..
    ....@@....
ENDMAP

NAME:    ossuary_tomb_2
# Generally have rooms full of either monsters or traps.
TAGS:    transparent uniq_ossuary
KFEAT:   ~ = arrow trap w:2 / needle trap
KFEAT:   ^ = arrow trap / floor w:5
# Number of 1's is 53, number of ~'s is 57.
: if crawl.one_chance_in(10) then
SUBST:   ~ = 3
SUBST:   3 = 3:20 2:10 ~:50 .:30
: else
SHUFFLE: 3~
SUBST:   3 = 3:20 2:10 .:20
SUBST:   ~ = ~:20 .:10
:end
SUBST:   z = 1
NSUBST:  B = 1:+ / *:c
NSUBST:  C = 1:+ / *:c
NSUBST:  D = 1:+ / *:c
NSUBST:  E = 1:+ / *:c
NSUBST:  F = 1:+ / *:c
NSUBST:  G = 1:+ / *:c
NSUBST:  H = 1:+ / *:c
NSUBST:  I = 1:+ / *:c
NSUBST:  J = 1:+ / *:c
NSUBST:  K = 1:+ / *:c
SUBST: ' = '''.
CLEAR: '
# loot: 12 items
ITEM:    any scroll w:6 / any potion / any potion q:2 w:2
: ossuary_setup_features(_G)
MAP
ccccccccccccccccccccc
c333H~~~G333F~~~E333c
c333H~~~G333F~~~E333c
c333H~~~G333F~~~E333c
cIIIcccccccccccccDDDc
c~~~c}'''''''''}c~~~c
c~~~c'''''''''''c~~~c
c~~~c'''1'''1'''c~~~c
c~~~c'''''''''''c~~~c
cJJJc'''''''''''cCCCc
c333c'1''*}|''1'c333c
c333c'''''''''''c333c
c333c'''''''''''c333c
c333c'''1'''1'''c333c
cKKKc'''''''''''cBBBc
c~~~c'''''''''''c^^^c
c~~~c+++cccccqqqc^^^c
c~~~c...cdddc...c.z^c
c~~~+1d1+***q...+.^^c
c~~~c...cdddc...c.z^c
cccccccccqqqc+++ccccc
             .@.
ENDMAP

NAME:    ossuary_tomb_3
ORIENT:  north
TAGS:    uniq_ossuary no_rotate no_hmirror no_vmirror
# zombies should be awake, so they march downwards in a line
NSUBST:  l = 2:l / .
CLEAR:   l
ITEM:    any scroll w:5 / any potion
: ossuary_setup_features(_G)
MAP
   ccccccccccccccc
  cc.....1}1.....cc
 cc..0...ccc...0..cc
 c......cc|cc......c
 c.....cc...cc.....c
 c......1...1......c
 cc..3..0.G.0..3..cc
  cc.............cc
   c+cc.......cc+c
  cc..cc22222cc..cc
 cc..3.cc.G.cc.3..cc
ccd....Hc...cI...3dcc
c$dd3..Hc.3.cI...dd$c
c$dd...Hc...cI...dd$c
c$$d.3.Hc.G.cI.3.d$$c
cc$dd..Hc...cI..dd$cc
 c$$d..cc.3.cc..d$$c
 cc$d.}cc...cc}.d$cc
  c$d.ccl.G.lcc.d$c
  cccccll.@.llccccc
ENDMAP

###########################################################
# Rare: WEIGHT 20
###########################################################

NAME:    minmay_ossuary_two_tombs
TAGS:    uniq_ossuary
# Loot: between 5 scrolls/potions and 9 scrolls/potions, with some gold.
# The tomb close to the player is a bit easier than the other one, and is
# more likely to be the one with less loot, giving weak players a chance.
SHUFFLE: def$
NSUBST:  d = 1:1 / *:d
NSUBST:  e = 1:1 / *:e
NSUBST:  $ = 1:1 / *:$
SUBST:   2 = 2 3:1 .:1
SUBST:   3 = 3 2:1 .:1
SUBST:   . = 23..
SUBST:   ' = 23.''
CLEAR:   'm
ITEM:    any scroll, any potion
: ossuary_setup_features(_G)
MAP
    '''''''''''
  ''''cccccccc''''
 ''cccc3223+ecccc''
 'cc.......ceec2cc'
 'c}.ccccc.cccc.+@ 
 'cc.+dddc.....2cc'
 ''ccccddc...cccc''
  ''''cccccccc''''
mmmm''''''''''''mmm
mmmmmmmmmmmmmmmmmmm
mmmmmmmmmmmmmmmmmmm
mmmmmmmmmmmmmmmmmmm
mmmmmmmmmmmmmmmmmmm
mmmmmmmmmmmmmmmmmmm
mmmmmmmmmmmmmmmmmmm
mmmmmmmmmmmmmmmmmmm
mmmmmmmmmmmmmmmmmmm
mmmmmmmmmmmmmmmmmmm
mmmmmmmmmmmmmmmmmmm
mmmmmmmmmmmmmmmmmmm
mmmmmmmmmmmmmmmmmmm
mmmmmmmmmmmmmmmmmmm
mmmmmmmmmmmmmmmmmmm
mmmmmmmmmmmmmmmmmmm
mmmmmmmmmmmmmmmmmmm
mmmmmmmmmmmmmmmmmmm
mmmmmmmmmmmmmmmmmmm
mmmmmmmmmmmmmmmmmmm
mmmmmmmmmmmmmmmmmmm
mmmmmmmmmmmmmmmmmmm
mmmmmmmmmmmmmmmmmmm
mmmmmmmmmmmmmmmmmmm
mmmmmmmmmmmmmmmmmmm
mmmmmmmmmmmmmmmmmmm
mmmmmmmmmmmmmmmmmmm
mmmmmmmmmmmmmmmmmmm
mmmmmmmmmmmmmmmmmmm
mmmm''''''''''''mmm
  ''''cccccccc''''
 ''cccc3+ffc3cccc''
 'cc2.2.cccc.c$$cc'
  @+.........+$$}c'
 'cc2.2.cccc.c$$cc'
 ''cccc3+ffc3cccc''
  ''''cccccccc''''
    ''''''''''''
ENDMAP

NAME:    ossuary_crypta
ORIENT:  north
TAGS:    uniq_ossuary no_rotate no_hmirror
# mix of good items with useless ones
ITEM:    scroll of random uselessness w:20 / scroll of immolation / \
         scroll of bless item / \
         potion of mutation w:5 / potion of ambrosia w:5 / \
         potion of degeneration / potion of lignification / \
         potion of curing w:15
# and sometimes mostly useless ones
ITEM:    scroll of attention / scroll of random uselessness w:20 / \
         scroll of immolation / potion of mutation w:5 / \
         potion of degeneration w:40
# occasionally just use this
ITEM:    any potion / any scroll
KITEM:   g = any manual / scroll of acquirement
SHUFFLE: hjkl
SUBST:   h:d, j:e, k:de, l:.de
NSUBST:  f = 6:f / *:e
NSUBST:  d = 1:} / 2:M / *:d
NSUBST:  e = 1:} / 2:M / *:e
NSUBST:  2 = 2:0 / *:2
SUBST:   M = 1f, d = dddf, e = eeef
NSUBST:   f = 2:g / *:f
SUBST:   ' = 44~...
SUBST:   4 = 23444
NSUBST:  ~ = 4:~ / *:.
KFEAT:   ~ = congregation trap
NSUBST:  . = 6:^ / *:.
KFEAT:   ^ = alarm trap / floor w:20
: ossuary_setup_features(_G)
MAP
         vvvvv
         vI}Hv
        vvfffvv
 ccccc vvfffffvv ccccc
 clllc vf1GfG1fv chhhc
 clllc vvvv^vvvv chhhc
 cc^cc    c^c    cc^cc
ccc.c   ccc+ccc   c.ccc
c2+.cc cc'''''cc cc.+2c
cccc.ccc'''''''ccc.cccc
 c2+..c'''''''''c..+2c
 cccI.+''''U''''+.Hccc
 c2+..c'''''''''c..+2c
cccc.ccc'''''''ccc.cccc
c2+.cc cc'''''cc cc.+2c
ccc.c   c@@@@@c   c.ccc
 cc^cc           cc^cc
 ckkkc           cjjjc
 ckkkc           cjjjc
 ccccc           ccccc
ENDMAP

# Scorpions, and lots of them. Hope you weren't expecting to breeze through
# the ossuary just because you found a mace of holy wrath!
NAME:   minmay_ossuary_scorpions_multiroom
TAGS:   uniq_ossuary
KFEAT:  ~ = needle trap / arrow trap / spear trap w:5
KITEM:  1 = *
SUBST:  4 = 44.
SUBST:  2 = 233.
SUBST: ' = '..
CLEAR: '
: ossuary_setup_features(_G)
MAP
ccccccccccccc
c4.1c4.2c2.4c
c...+...G...c
c2.1c2.....2c
cc+cccG.1.Gcc
c~~~c2.....2c
c~~~c...G...c
c~~~c4.2c2.4c
cc+ccccccc+cc
c424cd*dq~~~c'
c2}2+d1dq~~~c'
c424cd*dq~~~c''
ccccccccc+++c'''
     ''''@@@''''
      ''''''''''
       ''''''''
        ''''''
ENDMAP

NAME:   minmay_ossuary_scorpions_large_arena
TAGS:   uniq_ossuary
KFEAT:  ~ = needle trap / arrow trap / spear trap w:5
KITEM:  1 = *
SUBST:  4 = 44.
SUBST:  2 = 233.
SUBST: ' = '..
CLEAR: '
: ossuary_setup_features(_G)
MAP
   ccccccccccccccccc
   c4...2c4...4qdddc
   c.....c.....qd1dc
   c...........qdddc
   c...........cc+cc
   c2....4....2c424c
   cccG.414.Gccc2}2c
   c2....4....4c424c
  'c...........cc+cc
 ''c.....G.....c~~~c
 ''c.....c.....+~~~c
'''c2...2c4...4c~~~c
'''cc+++cccccccccccc
'''''@@@'''''''
'''''''''''''
''''''''''''
ENDMAP

# Another ossuary (loosely) based on Tomb:2
NAME:    ossuary_minmay_tomb_2a
TAGS:    transparent uniq_ossuary no_rotate no_hmirror no_vmirror
ITEM:    any scroll / any scroll q:2, any potion / any potion q:2
SHUFFLE: $de*
NSUBST:  B = 1:+ / *:n
NSUBST:  C = 1:+ / *:n
NSUBST:  D = 1:+ / *:n
NSUBST:  E = 1:+ / *:n
NSUBST:  X = 1:{ / *:>
SUBST: . = 4~.....p
NSUBST: ~ = 15:~ / *:4...
KFEAT: ~ = needle trap / net trap / spear trap
SUBST: 0 = 045
SUBST: 4 = 2344
: if crawl.coinflip() then
# Clearing it was letting nasty stuff that's not part of the vault appear too often.
SUBST: p = x
: end
: ossuary_setup_features(_G)
MAP
BBBBB                       CCCCC
B$$$B                       CdddC
B$$$B   .................   CdddC
B$$$B.......................CdddC
BBBBB.........ccccc.........CCCCC
   .......G...........G.......
   ...1.........0.........1... 
  .....cc....0.....0....cc.....
  ..J..cc...............cc..J..
  .........0.........0.........
  ..c.......................c..
  ..cc..........X..........cc..
  ..cI....0....{>{....0....Hc..
  ..cc..........X..........cc..
  ..c.......................c..
  .........0.........0.........
  ..J..........................
  .....cc....0.....0....cc..J..
  .....cc.......0.......cc.....
   ...1...................1... 
   .......G...........G...... 
EEEEE.........ccccc.........DDDDD
E***E...................... DeeeD
E***E   .................   DeeeD
E***E                       DeeeD
EEEEE                       DDDDD
ENDMAP

NAME:    ossuary_zaba_flooded
ORIENT:  east
TAGS:    uniq_ossuary
SHUFFLE: Yy% / Zz$ / Uu#
SUBST:   U : @, u# = rw.
SUBST:   YZyz : x, %$ : c
{{
local flood_x, flood_y, n = -1, -1, 0

-- Find where to start the flood.
flood_x = 7
for y = 0, height() - 1 do
    if mapgrd[7][y] == '.' then
        n = n + 1
        if crawl.one_chance_in(n) then
            flood_y = y
        end
    end
end

-- Flood it!
flood(flood_x, flood_y, crawl.random_range(12, 16), function(x, y, distance)
    local c = mapgrd[x][y]

    local replacements = {
        ['.'] = { {'r', 1}, {'.', 1}, {'W', 4} },
        ['x'] = { {'r', 1}, {'w', 5} },
        ['c'] = { {'c', 5}, {'w', 3}, {'W', 2}, {'x', 1} },
        ['+'] = { {'+', 10}, {'W', 5} },
        ['1'] = { {'2', 1} }
    }

    if replacements[c] then
        mapgrd[x][y] = util.random_choose_weighted(replacements[c])
    end

    if c == '+' or c == 'v' then
        return 0
    end

    return distance - 1
end)

}}
SHUFFLE: ABI/CDJ/EFK/GHL
SUBST:   A : 1, BD : }, IJ : |
SUBST:   EFKGHL : ddd*
SUBST:   C = 1112
SUBST:   2 = ppp!.
SUBST:   1 = 123
KFEAT:   r = floor
KMONS:   p = withered plant
ITEM:    any potion / any scroll / any jewellery w:1
: ossuary_setup_features(_G)
CLEAR:   x
COLOUR:  r = brown
FTILE:   r = FLOOR_SWAMP
MAP
      xxxxxxxxxxxxxxx
   xxxxxxxxxxxxxxxxxxxxx
  xxxxxxxxxxxxxxxxxxxxxxx
 xxxxxxxxxxxxxxxxxxxxxxxxx
 xxxxxcccccccccccccccxxxxx
 xxxxxc1c1c1c1c1c1c1cxxxxx
xxxxccc+c+c+c+c+c+c+cccxxxx
xxxxc1+.............+1cxxxx
Yyyy%%%.vv+vv+vv+vv.cccxxxx
xxxxc1+.vAAAv1vCCCv.+1cxxxx
xxxxccc.vABAvvvCDCv.cccxxxx
xxxxc1+.vAAIv1vJCCv.+1cxxxx
Zzzz$$$.vvvvv+vvvvv.cccxxxx
xxxxc1+......}......+1cxxxx
xxxxccc.vvvvv+vvvvv.cccxxxx
xxxxc1+.vEEKv1vLGGv.+1cxxxx
Uuuu###.vEFEvvvGHGv.cccxxxx
xxxxc1+.vEEEv1vGGGv.+1cxxxx
xxxxccc.vv+vv+vv+vv.cccxxxx
xxxxc1+.............+1cxxxx
xxxxccc+c+c+c+c+c+c+cccxxxx
 xxxxxc1c1c1c1c1c1c1cxxxxx
 xxxxxcccccccccccccccxxxxx
 xxxxxxxxxxxxxxxxxxxxxxxxx
  xxxxxxxxxxxxxxxxxxxxxxx
   xxxxxxxxxxxxxxxxxxxxx
      xxxxxxxxxxxxxxx
ENDMAP

###########################################################
# Very rare: WEIGHT 10
###########################################################

# Players have two choices: they can try to hang back and slowly fight the
# undead, or they can run to the gold relying on their speed/stealth.
#
# The former option is difficult since there's no choke point, whereas the
# latter option is inherently risky.  Gold averages a bit less than 200.
#
# Originally this vault had the monsters behind doors and an alarm trap
# before the gold, but then I found out that zombies can't open doors anymore.
NAME:   minmay_ossuary_gauntlet
ORIENT: east
TAGS:   uniq_ossuary
SUBST:  ' = '''''..
CLEAR:  '
NSUBST: 1 = 4:1 / *:2
: ossuary_setup_features(_G)
MAP
    '''''
  ''''''''''
  '''ccccccccccccccccc   ccccc
 ''''c1H1H1H1H1H1H1H1cccccHHHcc 
'''J'@................$$$$$$$Gc
'''''@...G...G...G...G$$$G$}$Gc
'''J'@................$$$$$$$Gc
 ''''c1I1I1I1I1I1I1I1cccccIIIcc
  '''ccccccccccccccccc   ccccc
  '''''''''''
    '''''
ENDMAP

NAME:    ossuary_the_hunt_dpeg
ORIENT: east
TAGS:   uniq_ossuary
# loot: 8 items of which 6 should be good.
# Most of the loot helps right here, if need be.
ITEM:    potion of curing / potion of heal wounds / potion of haste / \
         potion of ambrosia w:5 / potion of mutation w:5 / \
         scroll of teleportation w:5 / \
         scroll of blinking w:5 / scroll of holy word / \
         scroll of random uselessness
SUBST:   M = 111.
KFEAT:   ~ = net trap / alarm trap / congregation trap w:5 / floor w:100
KFEAT:   ^ = net trap w:20 / congregation trap w:5 / floor w:25
: ossuary_setup_features(_G)
MAP
           ccccccccccccccccccccc
 ccccccccccc2c2c2c2c2c2c2ccddddccc
cc1.....1.~~~~~~~~~~~~~~~~^....M}c
@=...G....~~~~~~~~~~~~~~~~^....M}c
cc1.....1.~~~~~~~~~~~~~~~~^....M}c
 ccccccccccc2c2c2c2c2c2c2ccddddccc
           ccccccccccccccccccccc
ENDMAP

NAME:    ossuary_due_mausoleum
ORIENT: south
TAGS:   uniq_ossuary
NSUBST:  = = 1:+ / *:c
SUBST:  . = ~3....
SUBST:  ' = .''''
KFEAT:  ~ = net trap / alarm trap / congregation trap w:5
CLEAR:  '
ITEM:    any scroll / any potion
: ossuary_setup_features(_G)
MAP
       '''J J'''
      ''''' '''''
    '''''''@'''''''
   '''ccccc+ccccc'''
  '''cc.........cc'''
 '''cc...cc=cc...cc'''
'''cc...cm.d.mc...cc'''
J''c...cm.ddd.mc...c''J
  @+...=.dd5dd.=...+@
J''c...cm.ddd.mc...c''J
'''cc...cm.}.mc...cc'''
 '''cc...cc=cc...cc'''
   ''cc.........cc''
    ''c..c+++c..c''
     'cccc333cccc'
      cdd+...+ddc    
      cddc222cddc
      cccc222cccc    
         c222c      
         c.1.c     
         c...c    
         c.}.c    
         c...c    
        cc+++cc    
       cc4...4cc    
      cc*.....*cc    
     cc|..111..|cc    
      cc*.....*cc     
       cc4.}.4cc     
        ccccccc
ENDMAP

# INDY: Oh, rats! ... He hates rats. He's scared to death of 'em.
NAME:    ossuary_due_small_catacomb
DEPTH:   D:3-, !D:$, Lair, !Lair:$
TAGS:    uniq_ossuary
ORIENT:  east
ITEM:    any scroll / any potion
# Loot: 10-19
# Some random content.
SHUFFLE: Bb/Zz, Xx/Ww, Ee/Vv
SUBST:   ZzWwVv : c, BXE : 1
SUBST:   3 = 666777
NSUBST:  b = 1:+ / c
NSUBST:  x = 1:+ / c
NSUBST:  e = 1:+ / c
# Guaranteed 10 items and 10 mummies, two in hidden rooms.
# 50/50 chance of loot for each of the eleven rats in the rat room.
SUBST:   x = 1
NSUBST:  ' = 10:^ / *:.
KFEAT:   ^ = spear trap
NSUBST:  y = 1:Y / *:y
SUBST:   ` = ddd*.........~
KFEAT:   ~ = alarm trap
NSUBST:  D = 3:} / 1:| / 2:* / *:d
KITEM:   Y = scroll of acquirement / any manual / potion of experience w:2
KMONS:   Y = hell hound
KITEM:   y = any scroll w:5 / any potion w:5 / nothing w:10
KMONS:   y = hell rat / spectral hell rat
: ossuary_setup_features(_G)
MAP
          ccccc
          cGDGc
         ccc1ccccc
       ccc.'''+''c      cccccc
      cc.'.ccccc'c     cc.+.3cc
     cc3.ccccccc+cc   cc..cc..cc
    cc..ccccccc```ccccc..cccc..ccc
    cc+ccccccy+`3`+.3''.cccccc.'.cc
 @ccc```ccccccb```ccc1cccccccecc'cc
 @.+``3`+ccccBcc+cccGDGccccceEec+ccccc
 @ccc```ccccccb..cccccccccc1ccc```ccGc
    cc+ccccccc..+cccccc.'+''''+`3`+1Dc
     c.cccccc1.cc'cxXx.3ccGDGcc```ccGc
     c1ccccc..cccc'cxx.ccccccccc+ccccc
     c..cccc+cccccc'cc+ccccccccc1cc
     cc..cc```cccccc+yyycccccccGDGc
      cc.+.`3`.+'''+yyyyy+.3ccccccc
       cccc```ccc3cccyyyccc..ccccccc
          cc+cccGDGccc+cc cc..ccccGc
          cc1ccccccc ccc   cc.''+1Dc
          cGDGc             ccccccGc
          ccccc                  ccc
ENDMAP

# Menkaure's pyramid
NAME:    ossuary_due_pyramid
TAGS:    no_rotate no_vmirror transparent uniq_ossuary
# Loot: 1-16 items
ITEM:    any scroll / any potion / w:5 nothing
KFEAT:   ^ = arrow trap w:20 / alarm trap
KFEAT:   m = spear trap
KFEAT:   ~ = alarm trap
NSUBST:  ' = 3:m / 30:2 / 30:3 / 30:^ / .
SUBST:    C = ccccpp.@
SUBST:   ` = .pp
CLEAR:    p
: ossuary_setup_features(_G)
MAP
                      ````
                     ``````
                    ````````
                   ````CC````
                  ````CCCC````
                 ````CCccCC````
                ````CC''''CC````
               ````CC''cc''CC````
              ````CC''cccc''CC````
             ````CC''cccccc''CC````
            ````CC''cc''''cc''CC````
           ````CC''cc''cc''cc''CC````
          ````CC''cc''cccc''cc''CC````
         ````CC''cc''ccGdcc''cc''CC````
        ````CC''cc''ccGdddcc''cc''CC````
       ````CC''cc''ccG1dd1dcc''cc''CC````
      ````CC''cc''ccG^d}dd..cc''cc''CC````
     ````CC''cc''ccG^d1dd1^'^cc''cc''CC````
    ````CC''cc''ccG^d^ddd^'~'~cc''cc''CC````
   ````CC''cc''cccccccccccccc''cc''cc''CC````
  ````CC''cc''''''''''''''''''''cc''cc''CC````
 ````CC''cccccccccccccccccccccccccc''cc''CC````
````CC''''''''''''''''''''''''''''''''cc''CC````
````CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC````
 ````CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC````
   ````````````````````````````````````````````
      `````````````````````````````````````
          `````````````````````````````
ENDMAP

NAME:   ossuary_zaba_two_paths
# There is no way up in the end. This is to give the zombies in the beginning
# more time to go for a walk and another chance to meet the player.
TAGS:   no_rotate uniq_ossuary
#
SUBST: 6 = NN6
KFEAT: N6 = floor
SUBST: 7 = OO7
# Loot.
ITEM:   any potion / any scroll
#
# There are not always traps in corridors but they are guaranteed near the loot:
KFEAT:  ' = arrow trap w:8 / alarm trap w:7 / floor w:85
KFEAT:  ~ = spear trap w:1 / net trap w:3 / arrow trap w:15
KITEM:  3 = $
SUBST: 2 = 22443
SUBST: p = ...p
CLEAR: p
: ossuary_setup_features(_G)
FTILE:  N6 = floor_demonic
COLOUR: N6 = green
FTILE:  O7 = floor_ice
COLOUR: O7 = cyan
MAP
  c+c+c+ccpppppppppppcc+c+c+c
ccc2c2c2cccpppppppppccc2c2c2ccc
+2ccccccc2+ppppppppp+2ccccccc2+
ccc2c2c2cccpppppppppccc2c2c2ccc
  c+c+c+cpppppppppppppc+c+c+c
   ppppppppppJpppJpppppppppp
   ppppppppppppppppppppppppp
  c=ccccccccccqqqcccccccccc=c
  b6cc3ccI3+.......+3Hcc3cc7v
  b66cc3ccIc.......cHcc3cc77v
  b666cc'ccc.30103.ccc'cc777v
  b6666cc'cc.......cc'cc7777v
  b66666cc'c.......c'cc77777v
  b666666c'+...G...+'c777777v
  b66666cc'c.......c'cc77777v
  b6666cc'cc.~~~~~.cc'cc7777v
  b666cc'ccc.~ddd~.ccc'cc777v
  b66cc'ccIc.~ddd~.cHcc'cc77v
  b6cc'ccI3+.~ddd~.+3Hcc'cc7v
  c+c+ccccccccccccccccccc+c+c
  c......c2c2c2c2c2c2c......c
  c222225c+c+c+c+c+c+c522222c
  ccccccccpppppppppppcccccccc
ENDMAP

NAME:    ossuary_bcadren_due_volcanic
DEPTH: D:3-, !D:$, Lair, !Lair:$, Spider, !Spider:$, Swamp, !Swamp:$
ORIENT:  south
TAGS:    uniq_ossuary
ITEM:    any scroll / any potion / any wand w:4
KFEAT:   Y = rock_wall
NSUBST:  5 = 4:5 / *:x
SUBST:   l = qlllj
NSUBST:  j = 6:J / *:l
NSUBST:  q = Qqqq 
KMONS:   q = flying skull w:40 / lava snake
KMONS:   Q = molten gargoyle w:2
NSUBST:  $ = 2:1 / 3:0 / *:2
: ossuary_setup_features(_G)
FTILE:   @J' = floor_pebble_red
COLOUR:  ' = red
KFEAT:   5 = rock_wall
KFEAT:   lQq = lava
TILE:    5x = wall_volcanic
MAP
YYYYYY@@@YYY
YxxxxYx@xYxYYYYY
Yxxxxxxx'xxxxxxYYYYY
Yxxxxxx5'xxxxxxxxxYYYYYYYYYY
Yxxxxx5x'5xxxxxxxxxxxxxxxxxYYYY
Yxxxxxx'xxxxxxxxxxxxxxxxxxxxxxYYYY
Yxxxx5'x5xxxxxxxxxxxxxxxxxxxxxxxxY
Yxxxxx''x5xxxxxxxxxxxxxxxxxxxxxxxY
Yxxxx5x'''xxxxxxxxxxxxxxxxxxxxxxxY
Yxx5xx'''x5xxxxxxxxxxxxxxxxxxxxccY
Yx5x''lll'xxcccccccccccccccccccccY
Yxx'llll'''Jc$c$c$c$c$ccI..1...HcY
Yx'lllllll''c+c+c+c+c+ccI.GdG.}HcY
Y5'llllllll'+..........+..4|4.}HcY
Yxx'llllll''c+c+c+c+c+ccI.GdG.}HcY
Yxx'lllll''Jc$c$c$c$c$ccI..1...HcY
Yxx'llll''xxcccccccccccccccccccccY
Yx5x'll''x5xxxxxxxxxxxxxxxxxxccccY
Yxx5x'''xxxxxxxxxxxxxxxxxxxxxxcccY
Yxxxx5xxxxxxxxxxxxxxxxxxxxxxxxxccY
YxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxY
ENDMAP

# BCADDO: Think about new walls and floors for this one
NAME: miniossuary_due_ghosttown
TAGS: uniq_miniossuary transparent
ITEM:    any scroll / any potion / any wand w:4
NSUBST:  A = 1:+ / *:c , B = 1:+ / *:c 
NSUBST:  C = 1:+ / *:c , D = 1:+ / *:c 
NSUBST:  E = 2:+ / *:c , F = 2:+ / *:c
NSUBST:  H = 2:+ / *:c , H = 2:+ / *:c
KFEAT:   + = closed_clear_door
NSUBST:  5 = 4:5 / 1:} / *:'
KMONS:   ' = wraith
KMONS:   } = guardian_mummy
KFEAT:   } = stone_stairs_down_i
: ossuary_setup_features(_G)
MAP
.............
.AAA.EEE.BBB.
.A5A.E5E.B5B.
.AAA.EEE.BBB.
.............
.FFF.ddd.HHH.
.F5F.dGd.H5H.
.FFF.ddd.HHH.
.............
.DDD.HHH.CCC.
.D5D.H5H.C5C.
.DDD.HHH.CCC.
.............
ENDMAP


# Four chambers, one with loot and the exit, three with zombies and a mummy.
# (The player won't know which is which because Crawl rotates the vault.)
#
# Stealthy players can close the doors without the mummy noticing. This is
# probably okay, but may turn out to be too easy.... -- dpeg
NAME:   ossuary_minmay_four_chambers
TAGS:   uniq_ossuary
SHUFFLE: ABC
SHUFFLE: sS / tT / uU / vV
SUBST: s = 3334........
SUBST: S = +
SUBST: tuv = '
SUBST: TUV = c
SUBST: A = 2
SUBST: B = 6
SUBST: C = 7
KFEAT: ~ = alarm trap / congregation trap
CLEAR: '
ITEM:   any scroll / any potion / any scroll q:2 w:2 / any potion q:2 w:2
: ossuary_setup_features(_G)
MAP
          ccc
         ccdcc
        ccdddcc
       sc.ddd.cv
       sc.d~d.cv
      sscc...ccvv
     sssscc.ccvvvv
   sssssssc+cvvvvvvv
  ccccssscc.ccvvvcccc
 ccAAccscS...VcvccBBcc
ccA..Accc..4..cccB..Bcc
cG1.~..+..4Y4..+..~.1Gc
ccA..Accc..4..cccB..Bcc
 ccAAcctcT...UcuccBBcc
  cccctttcc.ccuuucccc
   tttttttc+cuuuuuuu
     ttttcc.ccuuuu
      ttccC.Cccuu
       tcC.~.Ccu
       tcC...Ccu
        ccC1Ccc
         ccGcc
          ccc
ENDMAP

# The intent is that the mummies chase the player through the whole ossuary,
# forcing the player to either move quickly or soak up some death curses.
NAME:   ossuary_minmay_chase
ORIENT: east
TAGS:   uniq_ossuary no_rotate
ITEM:   any scroll / any potion / gold w:4
KFEAT:  ~ = arrow trap / needle trap / congregation trap w:4
KITEM: D = scroll of acquirement
SUBST: ' = 3..pppp
SUBST: 2 = 24
SUBST: 3 = 3334
CLEAR: p
: ossuary_setup_features(_G)
MAP
ccccc     ccccccccccc
cdddccccccc2G2G2G2G2cccccc
cdDd4.2.+...~.....~...+.4c
cdDd.4.2+...~.....~...+4.c
cdddccccccc2G2G2G2G2ccc++c
ccccc     ccccccccccc c..c
                     cc..cc
                     c2..2c
                     cI~~Hc
                     c2..2c
                     cI..Hc
                     c2..2c
                     cI..Hc
                     c2..2c
                     cI~~Hc
   '''''''''         c2..2c
 ''''''''''''''      cc..cc
 ccc@@@...ccccccccccccc..c
 c0c++ccccc2G2G2G2G2ccc++c
 c1.....+...~.....~...+4.c
 c1.....+...~.....~...+.4c
 c0cccccccc2G2G2G2G2cccccc
 ccc      ccccccccccc
ENDMAP

NAME:   ossuary_boxes_badplayer
TAGS:   uniq_ossuary
DEPTH:  D:3-, !D:$, Lair, !Lair:$, Spider, !Spider:$, Shoals, !Shoals:$
KFEAT:  01  = *
KFEAT:  ab = stone_wall
KITEM:  $  = any scroll / any potion / any wand w:4 / any ring w:2
SUBST: . = 44^.....
SUBST: 4 = 467
KFEAT: ^ = spear trap / needle trap
NSUBST: `  = 2:0 / 8:2 / 6:3 / *:.
NSUBST: 3  = 15:3 / 10:2 / *:. , 4 = 5:4 / *:.
NSUBST: a  = 1:+ / *:a , B = 1:+ / *:c , C = 1:+ / *:c , D = 1:+ / *:c
NSUBST: E  = 1:+ / *:c , F = 1:+ / *:c , G = 1:+ / *:c , H = 1:+ / *:c
NSUBST: I  = 1:+ / *:c , J = 1:+ / *:c , K = 1:+ / *:c , L = 1:+ / *:c
NSUBST: M  = 1:+ / *:c , } = 3:} / *:.
SUBST: l = ~...
KFEAT: ~ = alarm trap / congregation trap
TILE: ab = wall_tomb
{{
lua_marker("'", Triggerable.synchronized_markers(
  function_at_spot( "callback.badplayer_boxes_open_up",
                    { triggered = false }, true,
                    { only_at_slave = true, listen_to_slaves = true })))

lua_marker('ab', props_marker {disappearing_wall = 1})
}}
: ossuary_setup_features(_G)
MAP
```````````````````````````
`ccccc+ccc+ccc+ccc+ccc+ccc`
`c333c333c333c333c333c333c`
`c3cGcGGccHHHcIIIccJJcJc3c`
`+3G....c....c....c....J3c`
`ccc....c....c....c....ccc`
`c3G....c....c....c....J3+`
`c3G....b....c....b....J3c`
`+3ccccbbaaaacaaaabbcccc3c`
`ccc....a'''''''''a....ccc`
`c3F....a'lllllll'a....K3+`
`c3F....a'l}$$$}l'a....K3c`
`+3F....a'l$$1$$l'a....K3c`
`cccccccc'l$010$l'cccccccc`
`c3E....a'l$$1$$l'a....L3+`
`c3E....a'l}$$$}l'a....L3c`
`+3E....a'lllllll'a....L3c`
`ccc....a'''''''''a....ccc`
`c3ccccbbaaaacaaaabbcccc3+`
`c3D....b....c....b....M3c`
`+3D....c....c....c....M3c`
`ccc....c....c....c....ccc`
`c3D....c....c....c....M3+`
`c3cDcDDccCCCcBBBccMMcMc3c`
`c333c333c333c333c333c333c`
`ccc+ccc+ccc+ccc+ccc+ccccc`
```````````````````````````
ENDMAP

NAME:   ossuary_bcadren_kennysheep_ruined_city_of_the_dead
TAGS:   uniq_ossuary transparent
DEPTH: D:3-, !D:$, Lair, !Lair:$, Spider, !Spider:$
: if crawl.coinflip() then
  SUBST: A = '
  SUBST: B = .
: else
  SUBST: B = '
  SUBST: A = .
: end
: if crawl.coinflip() then
  SUBST: C = '
  SUBST: D = .
: else
  SUBST: D = '
  SUBST: C = .
: end
: if crawl.coinflip() then
  SUBST: E = '
  SUBST: F = .
: else
  SUBST: F = '
  SUBST: E = .
: end
: if crawl.coinflip() then
  SUBST: S = '
  SUBST: H = .
: else
  SUBST: H = '
  SUBST: S = .
: end
: if crawl.coinflip() then
  SUBST: I = '
  SUBST: J = .
: else
  SUBST: J = '
  SUBST: I = .
: end
: if crawl.coinflip() then
  SUBST: K = '
  SUBST: L = .
: else
  SUBST: L = '
  SUBST: K = .
: end
: if crawl.coinflip() then
  SUBST: M = '
  SUBST: N = .
: else
  SUBST: N = '
  SUBST: M = .
: end
: if crawl.coinflip() then
  SUBST: O = '
  SUBST: P = .
: else
  SUBST: P = '
  SUBST: O = .
: end
: if crawl.coinflip() then
  SUBST: Q = '
  SUBST: R = .
: else
  SUBST: R = '
  SUBST: Q = .
: end
: if crawl.coinflip() then
SUBST:  . = apwwww!xxx
SUBST:  ' = .Wa
KFEAT:  ! = deep_water
: else
SUBST:  . = xxxxxxx6
KFEAT:  6 = rock_wall
: end
KMONS:  p = withered plant
KFEAT:  . = needle trap / alarm trap / spear trap w:5 / floor w:200
SUBST:  3 = 3.
NSUBST: d = 3:5 / 3:1 / 4:3 / *:d
KFEAT:  5 = }
SUBST:  3 = 367
: ossuary_setup_features(_G)
KITEM:  15d = any potion / any scroll / any jewellery w:2
FTILE:  a = FLOOR_SWAMP
MAP
  ccc  ccc  ccc  ccc  ccc  
  cdc  cdc  cdc  cdc  cdc  
..c+c..c+c..c+c..c+c..c+cK@
.Kc@cJJc@cMMc@c..c@c.Pc@cKP
K.KLJ.JN.M..MRQQQ.QRP.POOPK
K.JJLLLJM.R.RMM..Q.PR..PPO@
@JccccCL3R.RcccM..3RHccccKO
.Jcd+@D3G3..cdc.M3G3S@+dcK.
.JccccEO3NJ.c+c.N.3.FccccK.
@J....OK.JNJc@cN...L....K..
@OOOOO.K...NJNKN...L....K..
.JccccKccccAN3NJccccLccccK.
.Jcd+@Ecd+@B3G3L@+dcA@+dcK.
@.ccccLccccCL3.KccccKccccK.
.......L.LLLLMNKKK..AKAKAK@
..DDDD..L...c@c.IIKAKAKAKA@
@DccccD.3.F.c+cI..3K.ccccB.
@Ecd+@C3G3.FcdcI.3G3A@+dcB.
.EccccD.3.SFcccI..3BBccccB.
..EEED....FS..H.IIH..BB.B..
..C.DE.E.F..SH.H.HIJ..JBA..
.Cc@cEEc@c..c@cHHc@cJJc@cA.
@Cc+c..c+c..c+c..c+c..c+c.@
  cdc  cdc  cdc  cdc  cdc
  ccc  ccc  ccc  ccc  ccc
ENDMAP

NAME:   miniossuary_kennysheep_ghosthouse
TAGS:   uniq_miniossuary no_hmirror no_rotate
ORIENT: south
NSUBST: ' = 4:5 / 2:~ / 5:3 2:1 / 2:* / *:.
SUBST:  3 = 367
KFEAT:  ~ = congregation trap
: ossuary_setup_features(_G)
MAP
          qq===qq
         qqI'''Hqq
        qqI'''''Hqq
       qqI'''''''Hqq
      qqI''''U''''Hqq
       qqI'''''''Hqq
        qqI'''''Hqq
         qqI}}}Hqq
          qqqqqqq
ENDMAP
